plugins {
    id 'signing'
    id "maven-publish"
    id "java-library"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group = "net.fornwall"
version = "2019.12.401"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

compileJava {
    options.release = 11
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
    afterTest { desc, result ->
        println "Test ${desc.name} [${desc.className}] result: ${result.resultType}"
    }
}

// See https://docs.gradle.org/current/userguide/publishing_maven.html
// and https://github.com/igr/repo-url-parser/blob/master/build.gradle
//
// Setup signing credentials with as described in
// https://docs.gradle.org/current/userguide/signing_plugin.html
// with the following gradle properties:
//    signing.keyId=XXX
//    signing.password=YYY
//    signing.secretKeyRingFile=ZZZ
//
// Then publish to local (~/.m2/) and remote with:
//    ./gradlew publishToMavenLocal
//    ./gradlew -PsonatypeUsername=XXX -PsonatypePassword=YYY publishMavenJavaPublicationToMavenRepository
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'aoc'
            from components.java
            pom {
                name = 'AdventOfCode'
                description = 'Advent of Code solver'
                url = 'https://github.com/fornwall/advent-of-code'
                licenses {
                  license {
                    name = 'The MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                  }
                }
                developers {
                    developer {
                        id = 'fornwall'
                        name = 'Fredrik Fornwall'
                        email = 'fredrik@fornwall.net'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/fornwall/advent-of-code.git'
                    developerConnection = 'scm:git:ssh://git@github.com/fornwall/advent-of-code.git'
                    url = 'http://github.com/fornwall/advent-of-code/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

